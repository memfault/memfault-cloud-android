apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

android {
    namespace 'com.memfault.cloud.sdk'
    compileSdkVersion versions.compileSdk

    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionName VERSION_NAME

        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField "String", "MEMFAULT_BASE_API_URL", "\"$MEMFAULT_BASE_API_URL\""
        buildConfigField "String", "MEMFAULT_BASE_CHUNKS_URL", "\"$MEMFAULT_BASE_CHUNKS_URL\""
        buildConfigField "String", "VERSION_NAME", "\"$VERSION_NAME\""
    }

    lintOptions {
        checkAllWarnings true
        warningsAsErrors true
        abortOnError true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation deps.kotlin.stdlib
    implementation deps.okio

    testImplementation deps.test.junitApi
    testImplementation deps.test.junitEngine
    testImplementation deps.test.kotlin.common
    testImplementation deps.test.kotlin.jdk
    testImplementation deps.test.json
    testImplementation deps.test.mockk
}

tasks.withType(org.jetbrains.dokka.gradle.DokkaTask).configureEach {
    outputDirectory.set(file("$buildDir/docs"))

    dokkaSourceSets {
        named("main") {
            includes.from("../README.md", "../LICENSE")
            reportUndocumented.set(true)
            includeNonPublic.set(false)

            perPackageOption {
                matchingRegex.set("com\\.memfault\\.cloud\\.sdk\\.internal.*")
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set("com\\.memfault\\.cloud\\.sdk\\.BuildConfig")
                suppress.set(true)
            }
        }
    }
}

apply from: 'gradle-mvn-push.gradle'
