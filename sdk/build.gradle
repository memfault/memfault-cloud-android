apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

android {
    namespace = "com.memfault.cloud.sdk"
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk

        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField "String", "MEMFAULT_BASE_API_URL", "\"$MEMFAULT_BASE_API_URL\""
        buildConfigField "String", "MEMFAULT_BASE_CHUNKS_URL", "\"$MEMFAULT_BASE_CHUNKS_URL\""
        buildConfigField "String", "VERSION_NAME", "\"$VERSION_NAME\""
    }

    buildFeatures {
        buildConfig = true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-opt-in=kotlin.time.ExperimentalTime"
        freeCompilerArgs += "-Xjvm-default=all"
        allWarningsAsErrors = true
    }

    lint {
        checkAllWarnings = true
        warningsAsErrors = true
        abortOnError = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation deps.kotlin.stdlib
    implementation deps.okio

    testImplementation deps.test.junitApi
    testImplementation deps.test.junitEngine
    testImplementation deps.test.kotlin.common
    testImplementation deps.test.kotlin.jdk
    testImplementation deps.test.json
    testImplementation deps.test.mockk
}

def copyDocsTask = tasks.register('copyDocs', Copy) {
    from "${rootProject.projectDir}/README.md"
    from "${rootProject.projectDir}/LICENSE"
    into "$buildDir/docs"
}

dokkaHtml {
    outputDirectory.set(file("$buildDir/docs"))
    dokkaSourceSets {
        configureEach {
            reportUndocumented.set(true)
            includeNonPublic.set(false)
            perPackageOption {
                matchingRegex.set("com.memfault.cloud.sdk.internal")
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set("com.memfault.cloud.sdk.BuildConfig")
                suppress.set(true)
            }
        }
    }

    finalizedBy {
        copyDocsTask
    }
}

apply from: 'gradle-mvn-push.gradle'
