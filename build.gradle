// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
        'kotlin': '2.1.0',
        'minSdk': 21,
        'compileSdk': 35,
        'appCompat': '1.7.0',
        'coreKtx': '1.15.0',
        'constraintLayout': '2.2.0',
        'dokka': '1.9.20',
        'androidGradlePlugin': '8.7.3',
        'json': '20240303',
        'junit': '5.11.4',
        'ktlintGradle' : '12.1.2',
        'lifecycleExtensions': '2.2.0',
        'leakCanary': '2.14',
        'mockk': '1.13.13',
        'okio': '3.9.1',
        'tape': '2.0.0-beta1',
        'viewmodelKtx': '2.8.7'
    ]
    ext.deps = [
        'android': [
            'appCompat': "androidx.appcompat:appcompat:${versions.appCompat}",
            'coreKtx': "androidx.core:core-ktx:${versions.coreKtx}",
            'constraintLayout': "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}",
            'gradlePlugin': "com.android.tools.build:gradle:${versions.androidGradlePlugin}",
            'lifecycleExtensions': "androidx.lifecycle:lifecycle-extensions:${versions.lifecycleExtensions}",
            'viewmodelKtx': "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.viewmodelKtx}",
        ],
        'kotlin': [
            'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
            'ktlintGradle' : "org.jlleitschuh.gradle:ktlint-gradle:${versions.ktlintGradle}",
            'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
        ],
        'dokka': "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}",
        'okio': "com.squareup.okio:okio:${versions.okio}",
        'leakCanary': "com.squareup.leakcanary:leakcanary-android:${versions.leakCanary}",
        'tape': "com.squareup.tape2:tape:${versions.tape}",
        'test': [
            'kotlin': [
                'common': 'org.jetbrains.kotlin:kotlin-test-common',
                'jdk': 'org.jetbrains.kotlin:kotlin-test-junit5',
            ],
            'mockk': "io.mockk:mockk:${versions.mockk}",
            'json': "org.json:json:${versions.json}",
            'junitApi' : "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
            'junitEngine' : "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
        ]
    ]
    repositories {
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }

    }
    dependencies {
        classpath deps.android.gradlePlugin
        classpath deps.kotlin.gradlePlugin
        classpath deps.kotlin.ktlintGradle
        classpath deps.dokka
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        version = "1.0.1"
        android = true
        outputColorName = "RED"
    }
}
